// Options for JavaCC.
options { LOOKAHEAD=1; FORCE_LA_CHECK=true; }

// Fonction principale
PARSER_BEGIN(Jagger)

import java.util.ArrayList;
import java.util.HashMap;

public class Jagger
{
    static Scope parent = null;

    public static void main(String args[]) throws Exception
    {
        Jagger parser = new Jagger(new java.io.FileReader(args[0]));
        parser.mainloop();
    }
}
PARSER_END(Jagger)

// Characters to ignore.
SKIP: { " " | "\r" | "\t" }

////////////////// Token specifications //////////////////
TOKEN:
{

    <PRINT: "print">                                                // Print
|   <LRB: "(">                                                      // Left round bracket
|   <RRB: ")">                                                      // Right round bracket

|    <IF: "if">
|    <THEN: "then">
|    <ELSE: "else">

|    <LET: "let">
|    <IN: "in">
|    <END: "end">
|   < COMMA: "," >
|    <VAR: "var">


|   <ASSIGN: ":=">                                                  // Assignement



|   < EOL: "\n" >                                                   // End of line.

|    < NUMBER: (<DIGIT>)+ ("." (<DIGIT>)*)? >                       // A decimal number.
|   < STRING: "\"" ((~["\n", "\r", "\""])*)? "\"" >                 // string
|   < DIGIT: ["0"-"9"] >                                            // A decimal digit.
|   < VARIABLE: ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z","0"-"9"])* >   // Variable
}

////////////////// Main lopp: read expressions on a line until end of file. //////////////////
//     mainloop -> (expression | scope)* <EOF>
void mainloop():
{ Expression a; }
{
    (
      (a=print()|a=scope()) (<EOL>)*{ new PrettyPrinter(a); System.out.println(); new Evaluator(a); parent=null;}
    )*
    <EOF>
}

////////////////// Statement //////////////////
//statement -> print | comparison
Expression statement():
{Token t; Expression a;}
{
 (t=<VARIABLE> <ASSIGN> a=comparison() { a=new Assignment(t.toString(), a, parent);} 
 |a = comparison()
 |a = print()
 )
 {return a;}
 }

 ////////////////// Scope //////////////////
 //  scope -> let (declaration)* in instruction (, instruction)* end
Expression scope():
{ Expression a; Scope s = new Scope(parent); parent=s; }
{
    <LET>
        (declaration())*
    <IN>
        a=statement() { s.addInstruction(a); } (<COMMA> a=statement() { s.addInstruction(a); })*
    <END>
    { return s; }
}

////////////////// Declaration //////////////////
//  declaration -> (var <VARIABLE> := comparison)*
void declaration():
{ Token t; Expression a; }
{
      <VAR> t=<VARIABLE> <ASSIGN> a=comparison() {parent.addDeclaration(t.toString(), a);} 
}

//Print.
// print -> print(comparison())
Expression print():
{Expression a;}
{
    <PRINT><LRB> a = comparison() <RRB> {return new Print(a);}
}

////////////////// Comparison //////////////////
// C -> E ('=='E | '<>'E | '>'E | '<'E | '>='E | '<='E)*
Expression comparison():
{ Expression a,b; }
{
    
    a=expression()
    (
      "=="  b=expression()  { a = new Equal(a,b); }
    | "<>"  b=expression()  { a = new NonEqual(a,b); }
    | ">"   b=expression()  { a = new Sup(a,b); }
    | "<"   b=expression()  { a = new Inf(a,b); }
    | ">="  b=expression()  { a = new SupEqual(a,b); }
    | "<="  b=expression()  { a = new InfEqual(a,b); }
    )?
    { return a; }
}

////////////////// Expression //////////////////
// E -> T ('+'T | '-'T)*
Expression expression():
{ Expression a,b; }
{
    a=term()
    (
      "+" b=term() { a=new Add(a,b); }
    | "-" b=term() { a=new Sub(a,b); }
    )? { return a; }
}

////////////////// Term //////////////////
// T -> U ('*'U | '/'U)*
Expression term():
{ Expression a,b; }
{
    a=unary()
    (
      "*" b=term() { a=new Mul(a,b); }
    | "/" b=term() { a=new Div(a,b); }
    )* { return a; }
}


//////////////////Unary expression//////////////////
// U -> "-" F | ("+")?F
Expression unary():
{Expression a;}
{
    (
    "-" a=prefactor() {a=new Neg(a);}
    | ("+")? a=prefactor()
    ){return a;}
}

////////////////// Factor //////////////////
// F -> <NUMBER> | "(" E ")"
Expression factor():
{ Token t; Expression a; }
{
      (t=<NUMBER> {a = new Num(Double.parseDouble(t.toString())); }
    | t=<STRING> { a = new Char(t.toString()); }
    | <LRB> a=comparison() <RRB>)
    { return a; }
   
}

////////////////// Prefactor //////////////////
// P -> (<IF> C <THEN> C <ELSE> C)|F|S|V)+
Expression prefactor():
{Expression a,b,c;}
{
    (<IF> a=comparison() <THEN> b=comparison() <ELSE> c=comparison() { a = new IfThenElse(a,b,c);}
    |a=factor()
    |a=scope()
    |a=variable()
    )+
    {return a;}
}

////////////////// Variable //////////////////
//V -> <VARIABLE>
Expression variable():
{ Token t; }
{
    t=<VARIABLE> { return new Variable(t.toString()); }
}